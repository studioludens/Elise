<?xml version="1.0" encoding="utf-8"?>
<mx:TextArea xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="creationCompleteHandler()">
     <mx:Script>
          <![CDATA[
			  
			  
 			   
 			   private function creationCompleteHandler():void {
 			   	
 			   		// initialize the event handlers
 			   		addEventListener( Event.CHANGE, changeHandler );
 			   		addEventListener( KeyboardEvent.KEY_DOWN, keyboardHandler );
 			   		
 			   		if (this.mx_internal::getTextField() != null && 
                         _autoResizable == true)
                         resizeTextArea();
 			   }
 			   
 			   private function changeHandler(e:Event):void {
 			   		if (this.mx_internal::getTextField() != null && 
                         _autoResizable == true)
                         resizeTextArea();
 			   }
 			   
 			   private function keyboardHandler(e:KeyboardEvent):void {
 			   		/*
 			   		if (this.mx_internal::getTextField() != null && 
                         _autoResizable == true)
                         resizeTextArea();
                    */
 			   }
               // auto resize setting
               private var _autoResizable:Boolean = false;
 
               // getter
               [Bindable(event="changeAutoResize")]
               public function get autoResize():Boolean
               {
                    return _autoResizable;
               }
 
               // setter
               public function set autoResize(b:Boolean):void
               {
                    _autoResizable = b;
                    // if the text field component is created
                    // and is auto resizable
                    // we call the resize method
                    if (this.mx_internal::getTextField() != null && 
                         _autoResizable == true)
                         resizeTextArea();
                    // dispatch event to make the autoResize 
                    // property bindable
                    dispatchEvent(new Event("changeAutoResize"));
               }
 
               // setter override
               override public function set text(value:String):void
               {
                    // calling super method 
                    super.text = value;
                    // if is auto resizable we call 
                    // the resize method
                    if (this.mx_internal::getTextField() != null && 
                         _autoResizable == true)
                         resizeTextArea();
               }
 
               // resize function for the text area
               private function resizeTextArea():void
               {
               		//trace("[ARTField] : resizeTextArea called");
               		if(this.mx_internal::getTextField() == null) return;
               		
               		//trace("[ARTField] : resizeTextArea execute");
                    // initial height value
                    // if set to 0 scroll bars will 
                    // appear to the resized text area 
                    var totalHeight:uint = 8;
                    // validating the object
                    this.validateNow();
                    // find the total number of text lines 
                    // in the text area
                    var noOfLines:int = this.mx_internal::getTextField().numLines;
                    // iterating through all lines of 
                    // text in the text area
                    
                    
                    for (var i:int = 0; i < noOfLines; i++) 
                    {
                         // getting the height of one text line
                         var textLineHeight:int = 
                             this.mx_internal::getTextField().getLineMetrics(i).height;
                         // adding the height to the total height
                         totalHeight += textLineHeight;
                    }
                    // setting the new calculated height
                    this.height = totalHeight;
                    
                    //trace("[ARTField] : resizeTextArea totalHeight:" + totalHeight);
                    //invalidateProperties();
               }
          ]]>
     </mx:Script>
</mx:TextArea>

